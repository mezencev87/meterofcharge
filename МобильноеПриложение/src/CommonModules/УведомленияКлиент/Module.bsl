#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций
// Процедура обработчик push-уведомлений и локальных уведомлений
	
Процедура ОбработкаУведомлений(Уведомление, Локальное, Показано, Параметры) Экспорт

	Если Уведомление.Данные = "1" Тогда
		ТекстВопроса = Уведомление.Текст + Символы.ПС + "Синхронизировать данные сейчас?";
		Оповещение = Новый ОписаниеОповещения("ОбработкаУведомленийЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	ИначеЕсли Локальное = Истина Тогда
//		ПараметрыФормы = Новый Структура("Текст, Данные", Уведомление.Текст, Уведомление.Данные);
//		ОткрытьФорму("ОбщаяФорма.Звонок", ПараметрыФормы);
	Иначе
		Если Не Показано Тогда
			ПоказатьПредупреждение( , Уведомление.Текст);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаУведомленийЗавершение(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекстОшибки = "";
		Если ОбменМобильноеПриложение.ВыполнитьОбменДанными(ТекстОшибки, Неопределено) ИЛИ 
				ОбменМобильноеПриложение.ЗарегистрироватьКаналСвязи(ТекстОшибки) Тогда
			ПоказатьПредупреждение( , "Синхронизация данных завершена.", 5);
			ОповеститьОбИзменении(Тип("ДокументСсылка.Расчет"));
			ОповеститьОбИзменении(Тип("СправочникСсылка.Абоненты"));
			ОповеститьОбИзменении(Тип("СправочникСсылка.Адреса"));
			ОповеститьОбИзменении(Тип("СправочникСсылка.НаборыУслуг"));
		Иначе
			ПоказатьПредупреждение( , ТекстОшибки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


// Процедура получает идентификатор подписчика уведомлений
// - получает новый идентификатор
// - отправляет идентификатор источнику push-уведомлений
// - информирует пользователя о возникших ошибках
Процедура ОбновитьИдентификаторПодписчикаУведомлений() Экспорт

#Если МобильноеПриложениеКлиент Тогда

	НомераПриложений = Новый Массив;
	НомераПриложений.Добавить("797785657074"); // Идентификатор приложения на промежуточном сервисе 1С

	Попытка
		ИдентификаторПодписчикаУведомлений = ДоставляемыеУведомления.ПолучитьИдентификаторПодписчикаУведомлений(
			НомераПриложений);
		ТекстОшибки = "";
		УведомленияСервер.ПередатьИдентификаторПодписчикаУведомлений(ИдентификаторПодписчикаУведомлений, ТекстОшибки);
		Если ТекстОшибки <> "" Тогда
			Сообщить(ТекстОшибки);
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		ПоказатьИнформациюОбОшибке(Инфо);
	КонецПопытки;

#КонецЕсли

КонецПроцедуры

#КонецОбласти

