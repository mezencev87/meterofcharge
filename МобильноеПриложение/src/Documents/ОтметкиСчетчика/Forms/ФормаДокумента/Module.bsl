#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьТекущиеПоказания();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеУведомлениями();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ОбновлениеДанных");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	СтрокаТЧ = Объект.Отметки[ИсточникВыбора.НомерСтроки - 1];
	СтрокаТЧ.ТекущиеПоказания = ИсточникВыбора.ТекущиеПоказания;
	Элементы.Отметки.ТекущаяСтрока = СтрокаТЧ.ПолучитьИдентификатор();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура НаборУслугПриИзменении(Элемент)
	НаборУслугПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УведомленияВключеныПриИзменении(Элемент)
	УправлениеУведомлениями();
КонецПроцедуры

&НаКлиенте
Асинх Процедура НаборУслугОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение <> Объект.НаборУслуг И Объект.Отметки.Количество() > 0 Тогда
		Ожидание = ВопросАсинх("Уже есть данные о показаниях, вы точно хотите изменить отслеживаемый ресурс?", РежимДиалогаВопрос.ДаНет);
		Результат = Ждать Ожидание;
		Если Результат = КодВозвратаДиалога.Да Тогда
			Объект.НаборУслуг = ВыбранноеЗначение;
		КонецЕсли;
	Иначе
		Объект.НаборУслуг = ВыбранноеЗначение;
	КонецЕсли;	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы //<ИмяТаблицыФормы>

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Добавить(Команда)
	ОбновитьТекущиеПоказания();
	НоваяСтрока = Объект.Отметки.Добавить();
	НоваяСтрока.ВремяОтметки = ТекущаяДата();
	НоваяСтрока.ПредыдущиеПоказания = ТекущиеПоказания;
	НоваяСтрока.ТекущиеПоказания = ТекущиеПоказания;
	стПараметры = Новый Структура("ВремяОтметки,ПредыдущиеПоказания,ТекущиеПоказания,СтрогийРежимОтметок,НомерСтроки");
	ЗаполнитьЗначенияСвойств(стПараметры, НоваяСтрока);
	стПараметры.СтрогийРежимОтметок = Объект.СтрогийРежимОтметок;
	ОткрытьФорму("Документ.ОтметкиСчетчика.Форма.ДобавлениеЗаписи", стПараметры, ЭтотОбъект);
	Объект.Дата = НоваяСтрока.ВремяОтметки;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьТекущиеПоказания()
	ТекущиеПоказания = 0;
	Для Каждого строка Из Объект.Отметки Цикл
		Если строка.ТекущиеПоказания > ТекущиеПоказания Тогда
			ТекущиеПоказания = строка.ТекущиеПоказания;
		КонецЕсли;
	КонецЦикла;
	Если ТекущиеПоказания = 0 И ЗначениеЗаполнено(Объект.НаборУслуг) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Максимум(РасчетыЗаУслугиОстатки.ПоказанияОстаток) КАК Показания
			|ИЗ
			|	РегистрНакопления.РасчетыЗаУслуги.Остатки(, НаборУслуг = &НаборУслуг) КАК РасчетыЗаУслугиОстатки";
		
		Запрос.УстановитьПараметр("НаборУслуг", Объект.НаборУслуг);
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ТекущиеПоказания = ВыборкаДетальныеЗаписи.Показания;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВремяУведомлений()
	Перем МассивВремен;
	МассивВремен = Новый Массив;
	МассивВремен.Добавить(НачалоДня(ТекущаяДатаСеанса()) + (Константы.ВремяУтреннихУведомлений.Получить() - Дата(1, 1, 1)));
	МассивВремен.Добавить(МассивВремен[0] + 3600*24);
	МассивВремен.Добавить(НачалоДня(ТекущаяДатаСеанса() + (Константы.ВремяВечернихУведомлений.Получить() - Дата(1, 1, 1))));
	МассивВремен.Добавить(МассивВремен[2] + 3600*24);
	Возврат МассивВремен;
КонецФункции

&НаКлиенте
Процедура УправлениеУведомлениями()
	Перем ВремяСледующегоУведомления;
	Перем Уведомление;
	Если НЕ НастройкиУведомленийЗаданы() И Объект.УведомленияВключены Тогда
		ОткрытьФорму("ОбщаяФорма.ВремяУведомлений");
	КонецЕсли;
#Если МобильноеПриложениеКлиент Тогда
	ДоставляемыеУведомления.ОтменитьЛокальныеУведомления();
	Если Объект.УведомленияВключены Тогда
		Для Каждого ВремяСледующегоУведомления Из ВремяУведомлений() Цикл
			Уведомление = Новый ДоставляемоеУведомление;
			Уведомление.Данные = СериализованнаяСсылкаДокумента();
			Уведомление.ДатаПоявленияУниверсальноеВремя = УниверсальноеВремя(ВремяСледующегоУведомления);
			Уведомление.Заголовок = "Отметки счетчика";
			Уведомление.Текст = "Пора делать отметку3";
			ДоставляемыеУведомления.ДобавитьЛокальноеУведомление(Уведомление);
		КонецЦикла;
	КонецЕсли;
#КонецЕсли
КонецПроцедуры

&НаСервере
Функция НастройкиУведомленийЗаданы()
	Если ЗначениеЗаполнено(Константы.ВремяВечернихУведомлений.Получить()) И 
			ЗначениеЗаполнено(Константы.ВремяУтреннихУведомлений.Получить()) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаСервере
Функция СериализованнаяСсылкаДокумента()
	Возврат СериализаторXDTO.XMLСтрока(Объект.Ссылка);
КонецФункции

&НаСервере
Процедура НаборУслугПриИзмененииНаСервере()
	ТекущаяСтрока = Неопределено;
	Если Объект.Отметки.Количество() > 0 Тогда
		Объект.Отметки.Сортировать("ТекущиеПоказания");
		ТекущаяСтрока = Объект.Отметки[Объект.Отметки.Количество() - 1];
		ТекущиеПоказания = ТекущаяСтрока.ТекущиеПоказания;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Максимум(РасчетыЗаУслугиОстатки.ПоказанияОстаток) КАК ПоказанияОстаток
		|ИЗ
		|	РегистрНакопления.РасчетыЗаУслуги.Остатки(, НаборУслуг = &НаборУслуг) КАК РасчетыЗаУслугиОстатки";
	
	Запрос.УстановитьПараметр("НаборУслуг", Объект.НаборУслуг);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ПоказанияПоРегистру = ВыборкаДетальныеЗаписи.ПоказанияОстаток;
		Если ПоказанияПоРегистру > ТекущиеПоказания И ТекущиеПоказания > 0 Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Текущие показания ниже, чем  зарегистрированные в приложении: " + ПоказанияПоРегистру;
			Сообщение.ПутьКДанным = "Отметки[" + ТекущаяСтрока.НомерСтроки + "].ТекущиеПоказания";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЕсли;
	Иначе
		ПоказанияПоРегистру = 0;
	КонецЕсли;
	Если ТекущаяСтрока <> Неопределено И ТекущиеПоказания = 0 Тогда
		ТекущаяСтрока.ТекущиеПоказания = ПоказанияПоРегистру;
		ТекущиеПоказания = ПоказанияПоРегистру;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти
