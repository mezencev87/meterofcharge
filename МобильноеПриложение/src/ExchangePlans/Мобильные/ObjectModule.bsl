#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

Процедура ЗаписатьСообщениеСИзменениями(Каталог) Экспорт

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Выгрузка в узел " + Строка(ЭтотОбъект) + " ------------";
		
	// Сформировать имя временного файла.
	ИмяФайла = Каталог + "Message" + СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + "_" + СокрЛП(Ссылка.Код) + ".xml";
	// Создать объект записи XML
	// *** ЗаписьXML-документов.
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// *** Инфраструктура сообщений.
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Ссылка);
	Сообщение.Текст = Сообщение.Текст + Символы.ПС + " Номер сообщения: " + ЗаписьСообщения.НомерСообщения;
	
	// Получить выборку измененных данных
	// *** Механизм регистрации изменений.
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель,ЗаписьСообщения.НомерСообщения);
	Пока ВыборкаИзменений.Следующий() Цикл
		Данные = ВыборкаИзменений.Получить();
		
		// Записать данные в сообщение *** XML-сериализация.
		ОбменМобильноеПриложение.ЗаписатьДанные(ЗаписьXML, Данные);
	КонецЦикла;

	ЗаписьСообщения.ЗакончитьЗапись();
	ЗаписьXML.Закрыть();
	
	Сообщение.Текст = Сообщение.Текст + Символы.ПС + "-------- Конец выгрузки ------------";
	Сообщение.Сообщить();

КонецПроцедуры 

Процедура ПрочитатьСообщениеСИзменениями(Каталог) Экспорт

	// Сформировать имя файла.
	ИмяФайла = Каталог + "Message" + СокрЛП(Ссылка.Код) + "_" + СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + ".xml";

	Файл = Новый Файл(ИмяФайла);
	Если Не Файл.Существует() Тогда
		Возврат;
	КонецЕсли;

	// *** Чтение документов XML	
	// Попытаться открыть файл.
	ЧтениеXML = Новый ЧтениеXML;
	Попытка 
		ЧтениеXML.ОткрытьФайл(ИмяФайла);

	Исключение 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Невозможно открыть файл обмена данными.";
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Загрузка из " + Строка(ЭтотОбъект) + " ------------";
	Сообщение.Текст = Сообщение.Текст + Символы.ПС + " – Считывается файл " + ИмяФайла;
	
	// Загрузить из найденного файла
	// *** Инфраструктура сообщений.
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();

	// Читать заголовок сообщения обмена данными – файла XML.
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);

	// Сообщение предназначено не для этого узла.
	Если ЧтениеСообщения.Отправитель <> Ссылка Тогда
		ВызватьИсключение "Неверный узел";
	КонецЕсли;

	// Удаляем регистрацию изменений для узла отправителя сообщения.
	// *** Служба регистрации изменений.
	ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

	// Читаем данные из сообщения *** XML-сериализация.
	Пока ОбменМобильноеПриложение.ВозможностьЧтенияДанных(ЧтениеXML) Цикл
		
		// Читаем очередное значение.
		Данные = ОбменМобильноеПриложение.ПрочитатьДанные(ЧтениеXML);

		// Не переносим изменение, полученное из офиса, если есть регистрация изменения на планшете
		Если Не ОбменМобильноеПриложение.ПринятьИзменения(ЧтениеСообщения.Отправитель, Данные) Тогда
			Сообщение.Текст = Сообщение.Текст + Символы.ПС + Строка(Данные.Метаданные()) + ": " + Строка(Данные) + 
							" – Изменения отклонены";		
									
			Продолжить;
		КонецЕсли;
		
		// Записать полученные данные.
		Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
		
	КонецЦикла;

	ЧтениеСообщения.ЗакончитьЧтение();
	ЧтениеXML.Закрыть();
	УдалитьФайлы(ИмяФайла);
	
	Сообщение.Текст =  Сообщение.Текст + Символы.ПС + "-------- Конец загрузки ------------";
	Сообщение.Сообщить();

КонецПроцедуры

#КонецОбласти


