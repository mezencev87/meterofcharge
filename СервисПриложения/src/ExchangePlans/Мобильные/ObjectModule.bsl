#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций
	
Процедура ЗаписатьСообщениеСИзменениями(Каталог) Экспорт

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Выгрузка в узел " + Строка(ЭтотОбъект) + " ------------";
	Сообщение.Сообщить();
		
	// Сформировать имя временного файла.
	ИмяФайла = Каталог + ?(Прав(Каталог, 1) = "\","", "\") + "Message" + СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + 
																		"_" + СокрЛП(Ссылка.Код) + ".xml";
	// Создать объект записи XML
	// *** ЗаписьXML-документов.
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// *** Инфраструктура сообщений.
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Ссылка);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = " Номер сообщения: " + ЗаписьСообщения.НомерСообщения;
	Сообщение.Сообщить();
	
	// Получить выборку измененных данных
	// *** Механизм регистрации изменений.
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель,ЗаписьСообщения.НомерСообщения);
	Пока ВыборкаИзменений.Следующий() Цикл
		Данные = ВыборкаИзменений.Получить();
		
		// Если перенос данных не нужен, то, возможно, необходимо записать удаление данных
		Если Не НуженПереносДанных(Данные) Тогда
			// Получаем значение с возможным удалением данных
			ОбменСервер.УдалениеДанных(Данные); 
		КонецЕсли;	

		// Записать данные в сообщение *** XML-сериализация.
		ЗаписатьXML(ЗаписьXML, Данные);
	КонецЦикла;

	ЗаписьСообщения.ЗакончитьЗапись();
	ЗаписьXML.Закрыть();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Конец выгрузки ------------";
	Сообщение.Сообщить();

КонецПроцедуры
 

Процедура ПрочитатьСообщениеСИзменениями(Каталог) Экспорт

	// Сформировать имя файла.
	ИмяФайла = Каталог + ?(Прав(Каталог, 1) = "\", "", "\") + "Message" + СокрЛП(Ссылка.Код) + "_" + 
													СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + ".xml";

	Файл = Новый Файл(ИмяФайла);
	Если Не Файл.Существует() Тогда
		Возврат;
	КонецЕсли;

	// *** Чтение документов XML	
	// Попытаться открыть файл.
	ЧтениеXML = Новый ЧтениеXML;
	Попытка 
		ЧтениеXML.ОткрытьФайл(ИмяФайла);

	Исключение 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Невозможно открыть файл обмена данными.";
		Сообщение.Сообщить();

		Возврат;

	КонецПопытки;

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Загрузка из " + Строка(ЭтотОбъект) + " ------------";
	Сообщение.Сообщить();
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = " – Считывается файл " + ИмяФайла;
	Сообщение.Сообщить();
	
	// Загрузить из найденного файла
	// *** Инфраструктура сообщений.
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();

	// Читать заголовок сообщения обмена данными – файла XML.
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);

	// Сообщение предназначено не для этого узла.
	Если ЧтениеСообщения.Отправитель <> Ссылка Тогда
		ВызватьИсключение "Неверный узел";
	КонецЕсли;

	// Удаляем регистрацию изменений для узла отправителя сообщения.
	// *** Служба регистрации изменений.
	ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);

	// Читаем данные из сообщения *** XML-сериализация.
	Пока ОбменСервер.ВозможностьЧтенияДанных(ЧтениеXML) Цикл
		
		// Читаем очередное значение.
		Данные = ОбменСервер.ПрочитатьДанные(ЧтениеXML);
		
		// Не переносим изменение, полученное от планшета, если есть регистрация изменения в офисе.
		Если Не ОбменСервер.ПринятьИзменения(ЧтениеСообщения.Отправитель, Данные) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = Строка(Данные.Метаданные()) + ": " + Строка(Данные) + " – Изменения отклонены";
			Сообщение.Сообщить();
									
			Продолжить;
		КонецЕсли;
		
		// Записать полученные данные.
		Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
		
	КонецЦикла;

	ЧтениеСообщения.ЗакончитьЧтение();
	ЧтениеXML.Закрыть();
	УдалитьФайлы(ИмяФайла);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "-------- Конец загрузки ------------";
	Сообщение.Сообщить();

КонецПроцедуры

Функция НуженПереносДанных(Данные) Экспорт
	
	Перенос = Истина;
	Если ТипЗнч(Данные) = Тип("СправочникОбъект.Адреса") Тогда
		
		// Проверяем, что у документов Абонент совпадает с реквизитом узла обмена
		Если Не Владелец.Пустая() И Данные.Владелец <> Владелец Тогда
			Перенос = Ложь;
		КонецЕсли;	
	ИначеЕсли ТипЗнч(Данные) = Тип("ДокументОбъект.Расчет") Тогда
		Если Не Владелец.Пустая() И Данные.Адрес.Владелец <> Владелец Тогда
			Перенос = Ложь;
		КонецЕсли;	
	ИначеЕсли ТипЗнч(Данные) = Тип("ДокументОбъект.ОтметкиСчетчика") Тогда
		Если Не Владелец.Пустая() И Данные.Адрес.Владелец <> Владелец Тогда
			Перенос = Ложь;
		КонецЕсли;	
	ИначеЕсли ТипЗнч(Данные) = Тип("СправочникОбъект.НаборыУслуг") Тогда
		Если Не Владелец.Пустая() И Данные.Владелец.Владелец <> Владелец Тогда
			Перенос = Ложь;
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(Данные) = Тип("СправочникОбъект.Абоненты") Тогда

		Если Не Владелец.Пустая() И Данные.Ссылка <> Владелец Тогда
			Перенос = Ложь;
		КонецЕсли;	
		
	КонецЕсли;
	Возврат Перенос;
	
КонецФункции

#КонецОбласти
#КонецЕсли
